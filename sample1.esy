PARM DEBUG(FLOW FLDCHK)
*
FILE PERSNL FB(150 1800) SYS010
NAME                17 16 A
LAST-NAME NAME          8 A
PAY-GROSS            94 4 P 2
DEPT                 98 3 N
DATE-OF-HIRE        136 6 N
HIRE-MM DATE-OF-HIRE    2 N
HIRE-DD DATE-OF-HIRE +2 2 N
HIRE-YY DATE-OF-HIRE +4 2 N
SALARY                W 4 P 2
BONUS                 W 4 P 2
RAISE                 W 4 P 2
SERVICE               W 2 N
CURR-DATE             S 6 N
CURR-MM CURR-DATE       2 N
CURR-DD CURR-DATE    +2 2 N
CURR-YY CURR-DATE    +4 2 N
*
FILE ERRPRINT PRINTER
*
JOB INPUT PERSNL
%GETDATE CURR-DATE
SALARY = PAY-GROSS * 52
PERFORM SERVICE-CALC
IF SERVICE LT 1
   GO TO JOB
END-IF
PERFORM RAISE-CALC
BONUS = 0
IF SERVICE GT 14
   PERFORM BONUS-CALC
END-IF
SALARY = SALARY + RAISE + BONUS
PRINT UPD-RPT
*
SERVICE-CALC. PROC
SERVICE = CURR-YY - HIRE-YY
IF CURR-MM < HIRE-MM
   SERVICE = SERVICE - 1
END-IF
IF CURR-MM NE HIRE-MM
   GOTO QUIT-SERV-CALC
END-IF
IF CURR-DD < HIRE-DD
   SERVICE = SERVICE - 1
END-IF
QUIT-SERV-CALC
END-PROC
*
RAISE-CALC. PROC
IF DEPT LT 940
   RAISE = SALARY * 0.1
ELSE
   RAISE = SALARY * 0.15
END-IF
END-PROC
*
BONUS-CALC. PROC
IF SALARY GT 29999
   DISPLAY ERRPRINT, LAST-NAME, +5, +
           'INELIGIBLE FOR BONUS'
   GOTO QUIT-BONUS
END-IF
IF SERVICE GT 19
   BONUS = 2000
ELSE
   BONUS = 1000
END-IF
PRINT BONUSRPT
QUIT-BONUS
END-PROC
*
REPORT UPD-RPT PAGESIZE 51 LINESIZE 63 NODATE NOPAGE
SEQUENCE DEPT LAST-NAME
CONTROL DEPT
TITLE 1 'ANNUAL UPDATE REPORT - SALARIED EMPLOYEES'
HEADING LAST-NAME 'NAME'
HEADING SERVICE 'SERV'
LINE DEPT LAST-NAME SERVICE RAISE SALARY
*
REPORT BONUSRPT LINESIZE 60 NODATE NOPAGE
SEQUENCE DEPT LAST-NAME
TITLE 1 'ANNUAL BONUS REPORT - SENIOR EMPLOYEES'
LINE DEPT LAST-NAME SERVICE BONUS
*
